/*
 *  lbully.lmn -- Leader Election (Bully Algorithm)
 *
 *  @author  Masato Gocho
 *  @date    2009.08.04
 *           2009.11.19  modified
 *
 */
 
 
% いわゆる最大値発見問題..かな

% property0: リーダーが同時に2人以上存在することはない


% Init phase
init{
  p_num=9. // # of processes
  _init0 @@ p_num(P)   :- int(P) |
            p_num(P,P).
  _init1 @@ p_num(P,M) :- P>0, Q=P-1 |
            p_num(Q,M), p{id(Q), st(normal), leader(-1)}.
}.
init_finalize @@
  bully{$b,@b}, init{p{$p,st(normal)}, p_num(0,P), $i,@i} :- int(P) |
  bully{$b,@b,$i, p{$p, st(elect)}}, p_num(P). % only 1st time, start @elect@

bully{
  phase(0).

% Election phase
  notic_ @@
    phase(0), crash{id(I), $p}, p{st(normal), leader(L), $q} :- I=:=L | 
    phase(0), crash{id(I), $p}, p{st(elect),  leader(L), $q}.
  elect1 @@
    phase(0), p{id(I), st(elect), $p}, p{id(J), st(normal), $q}  :- I < J |
    phase(0), p{id(I), st(lose), $p}, p{id(J), st(elect), $q}.
  elect2 @@
    phase(0), p{id(I), st(elect), $p}, p{id(J), st(lose), $q}  :- I < J |
    phase(0), p{id(I), st(lose), $p}, p{id(J), st(lose), $q}.

% Coodinate phase:
  iam_L @@
    phase(1), p{id(I), st(leadr),$p}, p{leader(L), st(S), $q}
                      :- I=\=L, unary(S), \+($p=($r,crash)), \+($q=($r,crash)) |
    phase(1), p{id(I), st(leadr),$p}, p{leader(I), st(normal), $q}.
}.

lead0 @@ bully{$b,@b, phase(0), p{id(I),st(elect), leader(L), $p}}/  :- int(I),int(L) |
         bully{$b,@b, phase(1), p{id(I),st(leadr), leader(I), $p}}.
lead1 @@ bully{$b,@b, phase(1)}/ :-
         bully{$b,@b, phase(0)}.

% leader crash!!
crash @@ bully{$b,@b, phase(0), p{$p, st(leadr), leader(L)}}/, p_num(P) :- int(L), Q=P-1 |
         bully{$b,@b, phase(0), crash{$p, st(-1), leader(-1)}}, p_num(Q).
//recvr @@ bully{$b,@b, phase(0), crash{$p, st(S)}}/, p_num(P) :- unary(S), Q=P+1 |
  //       bully{$b,@b, phase(0), p{$p, st(elect)}},  p_num(Q).